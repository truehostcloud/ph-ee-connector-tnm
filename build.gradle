plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.6'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'com.diffplug.spotless' version '6.19.0' apply false
}

group = 'org.mifos.connector'
sourceCompatibility = '17'

def camelCoreVersion = '3.19.0'
def zeebeClientVersion = '8.1.4'
def phConnectorCommonVersion = '1.5.1-SNAPSHOT'
def lombokVersion = '1.18.26'

repositories {
    mavenCentral()
    maven {
        url = uri('https://jfrog.sandbox.fynarfin.io/artifactory/fyn-libs-snapshot')
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "org.apache.camel.springboot:camel-spring-boot-starter:${camelCoreVersion}"
    implementation "io.camunda:zeebe-client-java:${zeebeClientVersion}"
    implementation "org.apache.camel.springboot:camel-jackson-starter:${camelCoreVersion}"
    implementation "org.apache.camel:camel-endpointdsl:${camelCoreVersion}"
    implementation "org.apache.camel:camel-undertow:${camelCoreVersion}"
    implementation "org.apache.camel:camel-http:${camelCoreVersion}"
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.0'
    implementation 'org.json:json:20220924'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "org.mifos:ph-ee-connector-common:${phConnectorCommonVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
allprojects {
    apply plugin: 'com.diffplug.spotless'

    // Configuration for the spotless plugin
    // https://github.com/diffplug/spotless/tree/main/plugin-gradle
    spotless {
        format 'misc', {
            target '**/*.md', '**/*.properties', '**/.gitignore', '**/.openapi-generator-ignore', '**/*.yml', '**/*.xml', '**/**.json', '**/*.sql'
            targetExclude '**/build/**', '**/bin/**', '**/.settings/**', '**/.idea/**', '**/.gradle/**', '**/gradlew.bat'
            indentWithSpaces(4)
            endWithNewline()
            trimTrailingWhitespace()
        }

        groovyGradle {
            target '*.gradle', '**/*.gradle'
            targetExclude '**/build/**'
            greclipse()
            indentWithSpaces(4)
            endWithNewline()
            trimTrailingWhitespace()
        }
        java {
            targetExclude '**/build/**', '**/bin/**', '**/out/**', '**/megalinter-reports/**'
            importOrder() //sort imports alphabetically
            removeUnusedImports()
            eclipse().configFile "$rootDir/config/dev-formatter.xml"
            endWithNewline()
            trimTrailingWhitespace()

            // Enforce style modifier order
            custom 'Modifier ordering', {
                def modifierRanking = [
                    public      : 1,
                    protected   : 2,
                    private     : 3,
                    abstract    : 4,
                    default     : 5,
                    static      : 6,
                    final       : 7,
                    transient   : 8,
                    volatile    : 9,
                    synchronized: 10,
                    native      : 11,
                    strictfp    : 12]
                // Find any instance of multiple modifiers. Lead with a non-word character to avoid
                // accidental matching against for instance, "an alternative default value"
                it.replaceAll(/\W(?:public |protected |private |abstract |default |static |final |transient |volatile |synchronized |native |strictfp ){2,}/, {
                    // Do not replace the leading non-word character.  Identify the modifiers
                    it.replaceAll(/(?:public |protected |private |abstract |default |static |final |transient |volatile |synchronized |native |strictfp ){2,}/, {
                        // Sort the modifiers according to the ranking above
                        it.split().sort({ modifierRanking[it] }).join(' ') + ' '
                    }
                    )
                }
                )
            }
        }

        lineEndings 'UNIX'
    }

    compileJava.dependsOn spotlessApply
}
tasks.named('test') {
    useJUnitPlatform()
}
